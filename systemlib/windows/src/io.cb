// TODO: use the widechar version of functions
import std::system

extern(C) {
const FILE_BEGIN = 0
const FILE_CURRENT = 1
const FILE_END = 2

const INVALID_HANDLE_VALUE = -1

type SECURITY_ATTRIBUTES = struct {
    nLength: int
    lpSecurityDescriptor: rawptr
    bInheritHandle: bool
}

type BY_HANDLE_FILE_INFORMATION = struct {
    dwFileAttributes: int
    ftCreationTime: int64
    ftLastAccessTime: int64
    ftLastWriteTime: int64
    dwVolumeSerialNumber: int
    nFileSizeHigh: int
    nFileSizeLow: int
    nNumberOfLinks: int
    nFileIndexHigh: int
    nFileIndexLow: int
}

func GetCommandLineA(): *pure uint8

func GetStdHandle(nStdHandle: int): rawptr

func WriteFile(handle: rawptr, str: *pure uint8, len: int, written: *int, ov: rawptr)

func ReadFile(
    hFile: rawptr,
    lpBuffer: rawptr,
    nNumberOfBytesToRead: int,
    lpNumberOfBytesRead: *int,
    lpOverlapped: rawptr
): bool

func CreateFileA(
    lpFileName: *pure uint8,
    dwDesiredAccess: int,
    dwShareMode: int,
    lpSecurityAttributes: *SECURITY_ATTRIBUTES,
    dwCreationDisposition: int,
    dwFlagsAndAttributes: int,
    hTemplateFile: rawptr
): rawptr

func SetFilePointer(
    hFile: rawptr,
    lDistanceToMove: int,
    lpDistanceToMoveHigh: *int,
    dwMoveMethod: int
): int

func CloseHandle(handle: rawptr): int

func GetFileInformationByHandle(handle: rawptr, lpFileInformation: &BY_HANDLE_FILE_INFORMATION): bool

func GetLastError(): int

func CopyFileA(path: *pure uint8, newpath: *pure uint8, failIfExists: bool): bool

func DeleteFileA(path: *pure uint8): bool

func MoveFileA(path: *pure uint8, newpath: *pure uint8): bool

func CreateDirectoryA(path: *pure uint8, lpSecurityAttributes: *SECURITY_ATTRIBUTES): bool

func RemoveDirectoryA(path: *pure uint8): bool

}

extern(carbon, std::system) {

func write(handle: rawptr, data: []pure uint8): isize {
    let written : int
    WriteFile(handle, cast(rawptr)data.ptr, cast(int)data.len, &written, nullptr)
    return written
}

func read(handle: rawptr, data: []uint8): isize {
    let bytes_read : int
    ReadFile(handle, cast(rawptr)data.ptr, cast(int)data.len, &bytes_read, nullptr)
    return bytes_read
}

func open(path: []pure uint8, flags: OpenFlags, mode: int): {rawptr, SystemError} {
    const GENERIC_READ = 1<<31
    const GENERIC_WRITE = 1<<30
    
    let access = GENERIC_READ
    if ((flags & (Append | Write)) != noflags) {
        access |= GENERIC_WRITE
    }

    const FILE_SHARE_READ = 1
    const FILE_SHARE_WRITE = 2
    let sharemode = FILE_SHARE_READ | FILE_SHARE_WRITE

    const OPEN_ALWAYS = 4
    const OPEN_EXISTS = 3
    let creation = OPEN_EXISTS
    if ((flags & Create) != noflags) {
        creation = OPEN_ALWAYS
    }

    const FILE_ATTRIBUTE_NORMAL = 128
    let attrs = FILE_ATTRIBUTE_NORMAL

    let err : SystemError
    let handle = CreateFileA(path.ptr, access, sharemode, nullptr, creation, attrs, nullptr)

    if (handle == cast(rawptr)(cast(uintptr)INVALID_HANDLE_VALUE)) {
        return nullptr, cast(SystemError) GetLastError()
    }

    if ((flags & Append) != noflags) {
        SetFilePointer(handle, 0, nullptr, FILE_END)
    }
    
    return handle, err
}

func seek(fd: rawptr, offset: int64, whence: Whence) {
    // TODO: handle large offset
    SetFilePointer(fd, cast(int) offset, nullptr, cast(int)whence)
}

func stat(fd: rawptr, buf: &Stat): SystemError {
    let info : BY_HANDLE_FILE_INFORMATION
    if (!GetFileInformationByHandle(fd, info)) {
        return cast(SystemError)GetLastError()
    }
    buf.size = info.nFileSizeHigh | info.nFileSizeLow
    buf.mtime = info.ftLastWriteTime
    buf.atime = info.ftLastAccessTime
    buf.ctime = info.ftCreationTime
    return NoError
}

func stat(filename: []pure uint8, buf: &Stat): SystemError {
    let handle, err = open(filename, noflags, 0)
    if (err != NoError) {
        return err
    }
    defer close(handle)

    // TODO: if (let err = stat(handle, buf); err != NoError)
    let err = stat(handle, buf)
    if (err != NoError) {
        return err
    }
    return NoError
}

func close(fd: rawptr): SystemError {
    return cast(SystemError)CloseHandle(fd)
}

const STD_INPUT_HANDLE = -10
const STD_OUTPUT_HANDLE = -11
const STD_ERROR_HANDLE = -12

func getStdinFd(): rawptr {
    return GetStdHandle(STD_INPUT_HANDLE)
}

func getStdoutFd(): rawptr {
    return GetStdHandle(STD_OUTPUT_HANDLE)
}

func getStderrFd(): rawptr {
    return GetStdHandle(STD_ERROR_HANDLE)
}

func unlink(path: []pure uint8): SystemError {
    if (!DeleteFileA(path.ptr)) {
        return cast(SystemError) GetLastError()
    }
    return NoError
}

func remove(path: []pure uint8): SystemError {
    if (!RemoveDirectoryA(path.ptr)) {
        return cast(SystemError) GetLastError()
    }
    return NoError
}

func rename(path: []pure uint8, dest: []pure uint8): SystemError {
    if (!MoveFileA(path.ptr, dest.ptr)) {
        return cast(SystemError) GetLastError()
    }
    return NoError
}

func copy(path: []pure uint8, dest: []pure uint8): SystemError {
    if (!CopyFileA(path.ptr, dest.ptr, false)) {
        return cast(SystemError) GetLastError()
    }
    return NoError
}

func mkdir(path: []pure uint8): SystemError {
    if (!CreateDirectoryA(path.ptr, nullptr)) {
        return cast(SystemError) GetLastError()
    }
    return NoError
}

}
