import std::system
import io

// TODO: make this work without casting the nil: (pwd.len > 0 then pwd.ptr else nil)

const STARTF_USESTDHANDLES = 0x00000100
const CREATE_NO_WINDOW     = 0x08000000

type STARTUPINFOA = struct {
    cb: int;
    lpReserved: &uint8;
    lpDesktop: &uint8;
    lpTitle: &uint8;
    dwX: int;
    dwY: int;
    dwXSize: int;
    dwYSize: int;
    dwXCountChars: int;
    dwYCountChars: int;
    dwFillAttribute: int;
    dwFlags: int;
    wShowWindow: int16;
    cbReserved2: int16;
    lpReserved2: &uint8;
    hStdInput: &opaque;
    hStdOutput: &opaque;
    hStdError: &opaque;
}

type PROCESS_INFORMATION = struct {
    hProcess: &opaque;
    hThread: &opaque;
    dwProcessId: int;
    dwThreadId: int;
}

extern(C) func ExitProcess(uExitCode: uint)

extern(C) func CreatePipe(
    hReadPipe: & &opaque,
    hWritePipe: & &opaque,
    lpPipeAttributes: &SECURITY_ATTRIBUTES,
    nSize: int
): bool

extern(C) func TerminateProcess(handle: &opaque, exitCode: uint): bool

extern(C) func CreateProcessA(
    lpApplicationName: &pure uint8,
    lpCommandLine: &pure uint8,
    lpProcessAttributes: &pure SECURITY_ATTRIBUTES,
    lpThreadAttributes: &pure SECURITY_ATTRIBUTES,
    bInheritHandles: bool,
    dwCreationFlags: int32,
    lpEnvironment: &opaque,
    lpCurrentDirectory: &uint8,
    lpStartupInfo: &pure STARTUPINFOA,
    lpProcessInformation: &PROCESS_INFORMATION
): bool

extern(carbon, std::system) {
func exit(code: int) {
    ExitProcess(code)
}

func readProcessChar(handle: &opaque, c: &uint8): bool {
    let dwRead : int
    let bSuccess = ReadFile( handle, cast(&opaque)c, 1, &dwRead, nil )
    return bSuccess && (dwRead == 1)
}

func process(cmdline: []pure uint8, pwd: []pure uint8): syserror {
    let outputHandleRead  : &opaque
    let outputHandleWrite : &opaque
    let inputHandleRead   : &opaque
    let inputHandleWrite  : &opaque

    let saAttr : SECURITY_ATTRIBUTES
    saAttr.nLength = sizeof(SECURITY_ATTRIBUTES)
    saAttr.bInheritHandle = true
    saAttr.lpSecurityDescriptor = nil

    if (!CreatePipe(&outputHandleRead, &outputHandleWrite, &saAttr, 0)) {
        return cast(syserror)GetLastError()
    }
    if (!CreatePipe(&inputHandleRead, &inputHandleWrite, &saAttr, 0)) {
        return cast(syserror)GetLastError()
    }

    let si : STARTUPINFOA
    si.cb = sizeof(si)
    si.dwFlags |= STARTF_USESTDHANDLES
    si.hStdOutput = outputHandleWrite
    si.hStdError = outputHandleWrite
    si.hStdInput = inputHandleRead

    let pi : PROCESS_INFORMATION
    let success = CreateProcessA(
        nil,
        cmdline.ptr,
        nil,
        nil,
        true,
        CREATE_NO_WINDOW,
        nil,
        (pwd.len > 0 then cast(&uint8)pwd.ptr else cast(&uint8)nil),
        &si,
        &pi
    )
    if (!success) {
        return cast(syserror)GetLastError()
    }

    // Close handles to the stdin and stdout pipes no longer needed by the child process.
    // If they are not explicitly closed, there is no way to recognize that the child process has ended.
    CloseHandle(outputHandleWrite)
    CloseHandle(inputHandleRead)

    let c : uint8
    while (readProcessChar(outputHandleRead, &c)) {
        write(stdout(), []pure uint8{&c, 1})
    }
    let err = cast(syserror)GetLastError()
    if (err != NOERROR && (cast(int)err) != 109) {
        return err 
    }

    CloseHandle(inputHandleWrite)
    CloseHandle(outputHandleRead)
    TerminateProcess(pi.hProcess, 0)

    // Close handles to the child process and its primary thread.
    CloseHandle(pi.hProcess)
    CloseHandle(pi.hThread)

    return NOERROR
}
}